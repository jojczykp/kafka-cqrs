plugins {
    id 'application'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.palantir.docker' version '0.34.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    //TODO Remove when split packages is no longer an issue
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath]
        classpath = files()
    }
}

application {
    mainClassName = 'pl.jojczykp.kafka_cqrs.persister.PersisterServiceMain'
}

docker {
    name 'jojczykp.pl/kafka-cqrs/persister-service:0.0.1'
    files tasks.distTar.outputs
}

clean.doFirst {
    delete fileTree(projectDir) { include '.attach_pid*' }
}

test {
    useJUnitPlatform()
    jvmArgs '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED' //TODO Cassandra 4 - Remove when no longer needed
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'org.projectlombok:lombok:1.18.22'

    implementation 'org.springframework.boot:spring-boot-starter:2.5.5'
    implementation 'org.springframework.kafka:spring-kafka:2.7.6'
    implementation 'org.springframework.data:spring-data-cassandra:3.2.4'
    implementation 'com.datastax.oss:java-driver-core:4.13.0'

    implementation 'com.google.guava:guava:31.0.1-jre'

    testImplementation project(':test-utils')
    testImplementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.5'
    testImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
    testImplementation 'org.spockframework:spock-junit4:2.0-groovy-3.0'
    testImplementation 'org.springframework.kafka:spring-kafka-test:2.7.6'
    testImplementation 'org.cassandraunit:cassandra-unit:4.3.1.0'
    testImplementation 'org.apache.cassandra:cassandra-all:4.0.1'            //TODO Cassandra 4 - Remove when version 4 included in cassandra-unit-spring
}

configurations {
    //TODO Remove when split packages is no longer an issue
    implementation.exclude group: 'com.google.code.findbugs', module: 'jsr305'
}
