plugins {
    id 'application'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.palantir.docker' version '0.34.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    //TODO Remove when split packages is no longer an issue
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath]
        classpath = files()
    }
}

application {
    mainClassName = 'pl.jojczykp.kafka_cqrs.notifier.NotifierServiceMain'
}

docker {
    name 'jojczykp.pl/kafka-cqrs/notifier-service:0.0.1'
    files tasks.distTar.outputs
}

clean.doFirst {
    delete "${projectDir}/.vertx/"
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'org.projectlombok:lombok:1.18.22'

    implementation 'org.springframework.boot:spring-boot-starter:2.5.5'
    implementation 'org.springframework.kafka:spring-kafka:2.7.6'
    implementation 'io.vertx:vertx-core:4.1.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
    implementation 'com.google.guava:guava:31.0.1-jre'

    testImplementation project(':test-utils')
    testImplementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.5'
    testImplementation 'org.springframework.kafka:spring-kafka-test:2.7.6'
    testImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
}

configurations {
    //TODO Remove when split packages is no longer an issue
    implementation.exclude group: 'com.google.code.findbugs', module: 'jsr305'
}
