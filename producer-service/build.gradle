plugins {
    id 'groovy'
    id 'application'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.palantir.docker' version '0.34.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13

    //TODO Remove when split packages is no longer an issue
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath]
        classpath = files()
    }
}

application {
    mainClassName = 'pl.jojczykp.kafka_cqrs.producer.ProducerServiceMain'
}

docker {
    name 'jojczykp.pl/kafka-cqrs/producer-service:0.0.1'
    files tasks.distTar.outputs
}

test {
    useJUnitPlatform()
}

clean.doFirst {
    delete fileTree(projectDir) { include '.attach_pid*' }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.google.guava:guava:31.1-jre'

    testImplementation 'org.codehaus.groovy:groovy:3.0.13'
    testImplementation 'org.codehaus.groovy:groovy-json:3.0.13'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-3.0'
}

configurations {
    //TODO Remove when split packages is no longer an issue
    implementation.exclude group: 'com.google.code.findbugs', module: 'jsr305'
}
